plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
}

apply from: "capacitor.build.gradle"

android {
    namespace "com.udry.app"
    compileSdkVersion 33
    defaultConfig {
        applicationId "com.udry.app"
        minSdkVersion 23
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    // Defines variables for accessing the keystore environment variables.
    def udryKeystorePath = System.getenv("UDRY_KEYSTORE_PATH") ?: "my-release-key.keystore"
    def udryStorePassword = System.getenv("UDRY_STORE_PASSWORD") ?: ""
    def udryKeyAlias = System.getenv("UDRY_KEY_ALIAS") ?: ""
    def udryKeyPassword = System.getenv("UDRY_KEY_PASSWORD") ?: ""

    signingConfigs {
        release {
            if (udryKeystorePath && new File(udryKeystorePath).exists()) {
                storeFile file(udryKeystorePath)
                storePassword udryStorePassword
                keyAlias udryKeyAlias
                keyPassword udryKeyPassword
            } else {
                println "Keystore not found at path: ${udryKeystorePath}. Using debug signing for release build."
                // Fallback to debug signing if release keystore is not found
                storeFile file('../debug.keystore')
                storePassword 'android'
                keyAlias 'androiddebugkey'
                keyPassword 'android'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    buildFeatures {
        buildConfig = true
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation(platform(libs.firebase.bom))
    implementation(libs.androidx.appcompat)
    implementation(libs.firebase.auth)
    implementation(libs.firebase.firestore)
    implementation project(':capacitor-android')
    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.espresso.core
    implementation project(':capacitor-app')
    implementation project(':capacitor-camera')
    implementation project(':capacitor-community-bluetooth-le')
    implementation project(':capacitor-community-sqlite')
    implementation project(':capacitor-status-bar')
}
